use bumpalo::Bump;

use pion_source::location::{BytePos, ByteRange};
use crate::tokens::Token;
use crate::syntax::*;
use crate::reporting::{TokenError, SyntaxError};

grammar<'arena, 'source>(
    builder: Builder<'arena>,
    arena: &'arena Bump,
    errors: &mut Vec<SyntaxError>,
);

extern {
    type Location = BytePos;
    type Error = TokenError;

    enum Token<'source> {
        "Ident" => Token::Ident(<&'source str>),
        "Hole" => Token::Hole(<&'source str>),
        "DecInt" => Token::DecInt(<&'source str>),
        "BinInt" => Token::BinInt(<&'source str>),
        "HexInt" => Token::HexInt(<&'source str>),

        "def" => Token::KwDef,
        "else" => Token::KwElse,
        "false" => Token::KwFalse,
        "fun" => Token::KwFun,
        "if" => Token::KwIf,
        "let" => Token::KwLet,
        "match" => Token::KwMatch,
        "then" => Token::KwThen,
        "true" => Token::KwTrue,

        "_" => Token::Underscore,
        "->" => Token::ThinArrow,
        "," => Token::Comma,
        ";" => Token::Semicolon,
        ":" => Token::Colon,
        "." => Token::Dot,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LCurly,
        "}" => Token::RCurly,
        "@" => Token::At,
        "=" => Token::Eq,
        "=>" => Token::FatArrow,
    }
}

pub Module: Module<'arena> = {
    <items: Item*> => Module { items: arena.alloc_slice_fill_iter(items) }
}

Item: Item<'arena> = {
    <def: Def> => Item::Def(def),
}

Def: Def<'arena> = {
    <start: @L> "def" <name: Ident> <r#type: Ann?> "=" <expr: Expr> ";" <end: @R> => builder.def((start, end), name, r#type, expr),
}

pub Expr: Expr<'arena> = {
    <start: @L> <expr: LetExpr> ":" <r#type: LetExpr> <end: @R> => builder.ann((start, end), expr, r#type),
    LetExpr => <>,
}

LetExpr: Expr<'arena> = {
    <start: @L> "let" <def: LetDef> ";" <body: LetExpr> <end: @R> => builder.r#let((start, end), def, body),
    <start: @L> "if" <cond: LetExpr> "then" <then: LetExpr> "else" <r#else: LetExpr> <end: @R> => builder.if_then_else((start, end), cond, then, r#else),
    FunExpr => <>,
}

LetDef: LetDef<'arena> = {
    <pat: Pat> <r#type: Ann?> "=" <expr: Expr> => LetDef {pat, r#type, expr},
}

FunExpr: Expr<'arena> = {
    <start: @L> <plicity: Plicity> <arg: AppExpr> "->" <body: FunExpr> <end: @R> => builder.arrow((start, end), plicity, arg, body),
    <start: @L> "fun" <params: Param+> "->" <body: FunExpr> <end: @R> => builder.fun_type((start, end), arena.alloc_slice_fill_iter(params), body),
    <start: @L> "fun" <params: Param+> "=>" <body: LetExpr> <end: @R> => builder.fun_lit((start, end), arena.alloc_slice_fill_iter(params), body),
    AppExpr => <>,
}

AppExpr: Expr<'arena> = {
    <start: @L> <fun: ProjExpr> <args: Arg+> <end: @R> => builder.fun_app((start, end), fun, arena.alloc_slice_fill_iter(args)),
    ProjExpr => <>,
}

ProjExpr: Expr<'arena> = {
    <start: @L> <head: AtomExpr> <labels: ("." <RangeIdent>)+> <end: @R> => builder.record_proj((start, end), head, arena.alloc_slice_fill_iter(labels)),
    AtomExpr => <>,
}

AtomExpr: Expr<'arena> = {
    <start: @L> "(" <expr: Expr> ")" <end: @R> => builder.paren((start, end), expr),
    <start: @L> <exprs: Tuple<Expr>> <end: @R> => Expr::TupleLit(ByteRange::new(start, end), exprs),
    <start: @L> <lit: Lit> <end: @R> => Expr::Lit(ByteRange::new(start, end), lit),
    <start: @L> "_" <end: @R> => Expr::Placeholder(ByteRange::new(start, end)),
    <start: @L> <hole: Hole> <end: @R> => Expr::Hole(ByteRange::new(start, end), hole),
    <start: @L> <ident: Ident> <end: @R> => Expr::Ident(ByteRange::new(start, end), ident),
    <start: @L> "{" "}" <end: @R> => Expr::TupleLit(ByteRange::new(start, end), &[]),
    <start: @L> "{" <fields: Seq1<TypeField, ",">> "}" <end: @R> => Expr::RecordType(ByteRange::new(start, end), fields),
    <start: @L> "{" <fields: Seq1<ExprField, ",">> "}" <end: @R> => Expr::RecordLit(ByteRange::new(start, end), fields),
    <start: @L> "match" <scrut: ProjExpr> "{" <cases: Seq<MatchCase, ",">> "}" <end: @R> => Expr::Match(ByteRange::new(start, end), arena.alloc(scrut), cases),
}

TypeField: TypeField<'arena> = {
    <label: RangeIdent> ":" <r#type: Expr> => TypeField {label, r#type},
}

ExprField: ExprField<'arena> = {
    <label: RangeIdent> "=" <expr: Expr> => ExprField {label, expr},
}

PatField: PatField<'arena> = {
    <label: RangeIdent> "=" <pat: Pat> => PatField {label, pat},
}

MatchCase: MatchCase<'arena> = {
    <pat: Pat> "=>" <expr: Expr> => MatchCase {pat, expr},
}

Param: Param<'arena> = {
    <plicity: Plicity> <pat: Pat> => Param {plicity, pat, r#type: None},
    "("<plicity: Plicity> <pat: Pat> <r#type: Ann> ")" => Param {plicity, pat, r#type: Some(r#type)},
}

Arg: Arg<'arena> = {
    <start: @L> <plicity: Plicity> <expr: ProjExpr> <end: @R> => Arg {extra: ByteRange::new(start, end), plicity, expr},
}

#[inline]
Plicity: Plicity = {
    () =>  Plicity::Explicit,
    "@" => Plicity::Implicit,
}

pub Pat: Pat<'arena> = {
    <start: @L> "(" <pat: Pat> ")" <end: @R> => Pat::Paren(ByteRange::new(start, end), arena.alloc(pat)),
    <start: @L> <lit: Lit> <end: @R> => Pat::Lit(ByteRange::new(start, end), lit),
    <start: @L> <ident: Ident> <end: @R> => Pat::Ident(ByteRange::new(start, end), ident),
    <start: @L> "_" <end: @R> => Pat::Underscore(ByteRange::new(start, end)),
    <start: @L> <pats: Tuple<Pat>> <end: @R> => Pat::TupleLit(ByteRange::new(start, end), pats),
    <start: @L> "{" <fields: Seq<PatField, ",">> "}" <end: @R> => Pat::RecordLit(ByteRange::new(start, end), fields),
}

pub Lit: Lit = {
    <start: @L> "true" <end: @R> => Lit::Bool(ByteRange::new(start, end), true),
    <start: @L> "false" <end: @R> => Lit::Bool(ByteRange::new(start, end), false),
    <start: @L> <int: Int> <end: @R> => Lit::Int(ByteRange::new(start, end), int),
}

Int: u32 = {
    <start: @L> <str: "DecInt"> <end: @R> => match parse_decimal_integer(str) {
        Ok(int) => int,
        Err(err) => {
            errors.push(SyntaxError::IntLit(ByteRange::new(start, end), err));
            0
        }
    },
    <start: @L> <str: "BinInt"> <end: @R> => match parse_binary_integer(str) {
        Ok(int) => int,
        Err(err) => {
            errors.push(SyntaxError::IntLit(ByteRange::new(start, end), err));
            0
        }
    },
    <start: @L> <str: "HexInt"> <end: @R> => match parse_hexadecimal_integer(str) {
        Ok(int) => int,
        Err(err) => {
            errors.push(SyntaxError::IntLit(ByteRange::new(start, end), err));
            0
        }
    },
}

Tuple<Elem>: &'arena [Elem] = {
    "(" ")" => &[],
    "(" <elem: Elem> "," ")" => arena.alloc_slice_fill_iter([elem]),
    "(" <elems: Seq2<Elem, ",">> ")" => elems,
};

Ann: Expr<'arena> = {
    ":" <expr: LetExpr> => expr,
}

Ident: Symbol = {
    <ident: "Ident"> => ident.into(),
}

RangeIdent: (ByteRange, Symbol) = {
    <start: @L> <ident: "Ident"> <end: @R> => (ByteRange::new(start, end), ident.into()),
}

Hole: Symbol = {
    <hole: "Hole"> => hole.into(),
}

Seq<Elem, Sep>: &'arena [Elem] = {
    <elems: (<Elem> Sep)*> <last: Elem?> => {
        let count = elems.len() + usize::from(last.is_some());
        let mut iter = elems.into_iter().chain(last);
        arena.alloc_slice_fill_with(count, |_| iter.next().unwrap())
    }
};

Seq1<Elem, Sep>: &'arena [Elem] = {
    <first: Elem> <elems: (Sep <Elem>)*> Sep? => {
        let count = elems.len() + 1;
        let mut iter = std::iter::once(first).chain(elems);
        arena.alloc_slice_fill_with(count, |_| iter.next().unwrap())
    },
};

Seq2<Elem, Sep>: &'arena [Elem] = {
    <first: Elem> Sep <second: Elem> <elems: (Sep <Elem>)*> Sep? => {
        let count = elems.len() + 2;
        let mut iter = std::iter::once(first).chain(std::iter::once(second)).chain(elems);
        arena.alloc_slice_fill_with(count, |_| iter.next().unwrap())
    },
};
